buildscript {
    ext.serialization_version = '0.12.0'
    ext.jline_version = '3.12.1'
    ext.gversion_version = '1.4.0'
    ext.lsp4j_version = '0.7.2'
    ext.coroutines_version = '1.3.0'
}

plugins {
    id 'org.jetbrains.kotlin.jvm' version '1.3.50'
    id 'kotlinx-serialization' version '1.3.50'
    id 'com.peterabeles.gversion' version '1.4.0'
    id 'com.github.johnrengelman.shadow' version '5.0.0'
    id 'application'
}

repositories {
    jcenter()
    mavenCentral()
    maven { url "https://kotlin.bintray.com/kotlinx" }
}

wrapper {
    distributionType = Wrapper.DistributionType.ALL
    gradleVersion = "5.6.1"
}

group = 'org.cth.klisp'
version = '0.0.1'
mainClassName = 'klisp.KlispKt'
sourceCompatibility = 9

compileKotlin.destinationDir = compileJava.destinationDir

dependencies {
    implementation 'org.jetbrains.kotlin:kotlin-stdlib'
    implementation 'org.jetbrains.kotlin:kotlin-stdlib-jdk8'
    implementation 'org.jetbrains.kotlin:kotlin-reflect'

    implementation "org.jetbrains.kotlinx:kotlinx-serialization-runtime:$serialization_version"
    implementation "org.jetbrains.kotlinx:kotlinx-coroutines-core:$coroutines_version"

    implementation "org.jline:jline-terminal-jansi:$jline_version"
    implementation "org.jline:jline-reader:$jline_version"
    implementation "org.jline:jline-remote-ssh:$jline_version"

    implementation "org.eclipse.lsp4j:org.eclipse.lsp4j:$lsp4j_version"
    implementation "org.eclipse.lsp4j:org.eclipse.lsp4j.jsonrpc:$lsp4j_version"

    testImplementation 'org.jetbrains.kotlin:kotlin-test'
    testImplementation 'org.jetbrains.kotlin:kotlin-test-junit'
}

compileJava {
    dependsOn(':compileKotlin')
    doFirst {
        options.compilerArgs = [
            '--module-path', classpath.asPath,
        ]
        classpath = files()
    }
}

tasks.withType(org.jetbrains.kotlin.gradle.tasks.KotlinCompile).all {
    kotlinOptions {
        jvmTarget = "9"
        apiVersion = "1.3"
        languageVersion = "1.3"
        javaParameters = true
    }
}

gversion {
    srcDir = "src/main/kotlin"
    classPackage = "klisp"
    className = "version"
    dateFormat = "yyyy-MM-dd'T'HH:mm:ss'Z'"
    timeZone = "UTC"
    debug = false
    language = "kotlin"
}

shadowJar {
    baseName = 'klisp'
    classifier = null
    version = null
}
