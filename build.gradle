buildscript {
    ext.serialization_version = '0.11.1'
    ext.jline_version = '3.11.0'
    ext.gversion_version = '1.4.0'
    ext.apache_sshd_version = '2.2.0'
}

plugins {
    id 'org.jetbrains.kotlin.jvm' version '1.3.41'
    id 'kotlinx-serialization' version '1.3.41'
    id 'com.peterabeles.gversion' version '1.4.0'
    id 'com.github.johnrengelman.shadow' version '5.0.0'
    id 'application'
}

repositories {
    jcenter()
    mavenCentral()
    maven { url "https://kotlin.bintray.com/kotlinx" }
}

group = 'org.cth.klisp'
version = '0.0.1'
mainClassName = 'klisp.KlispKt'

dependencies {
    implementation 'org.jetbrains.kotlin:kotlin-stdlib-jdk8'
    implementation 'org.jetbrains.kotlin:kotlin-reflect'
    implementation "org.jline:jline-terminal-jansi:$jline_version"
    implementation "org.jline:jline-reader:$jline_version"
    implementation "org.apache.sshd:sshd-core:$apache_sshd_version"
    implementation "org.apache.sshd:sshd-scp:$apache_sshd_version"
    implementation "org.apache.sshd:sshd-sftp:$apache_sshd_version"
    implementation "org.jline:jline-remote-ssh:$jline_version"
    implementation "org.jetbrains.kotlinx:kotlinx-serialization-runtime:$serialization_version"

    testImplementation 'org.jetbrains.kotlin:kotlin-test'
    testImplementation 'org.jetbrains.kotlin:kotlin-test-junit'
}

tasks.withType(org.jetbrains.kotlin.gradle.tasks.KotlinCompile).all {
     dependsOn(createVersionFile)
    kotlinOptions {
        jvmTarget = "1.8"
        apiVersion = "1.3"
        languageVersion = "1.3"
        javaParameters = true
    }
}

gversion {
    srcDir = "src/main/kotlin"
    classPackage = "klisp"
    className = "version"
    dateFormat = "yyyy-MM-dd'T'HH:mm:ss'Z'"
    timeZone = "UTC"
    debug = false
    language = "kotlin"
}

shadowJar {
    baseName = 'klisp'
    classifier = null
    version = null
    minimize()
}
